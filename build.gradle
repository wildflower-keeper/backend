plugins {
    id 'java'
    id 'java-library'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.springframework.boot' version '3.2.2'
}

jar { enabled = false }
bootJar { enabled = false }

repositories {
    mavenCentral()
}

subprojects {
    group = 'org.wildflowergardening'
    version = '1.0.0'
    tasks.register("prepareKotlinBuildScriptModel") {}
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    java { sourceCompatibility = '17' }

    tasks.named('test', Test) {
        useJUnitPlatform()
        testLogging {
            exceptionFormat = "full"
        }
    }

    configurations {
        implementation.exclude module: 'spring-boot-starter-logging'
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        implementation 'org.springframework.boot:spring-boot-starter-webflux'
        implementation 'org.springframework.boot:spring-boot-starter-log4j2'
        implementation 'mysql:mysql-connector-java:8.0.33'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.apache.poi:poi-ooxml:5.2.5'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor(
                'javax.annotation:javax.annotation-api:1.3.2',
                'org.projectlombok:lombok:1.18.24'
        )
        testAnnotationProcessor('org.projectlombok:lombok:1.18.24')
        testImplementation 'org.springframework.boot:spring-boot-starter-test:3.2.2'
        testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testImplementation "org.mockito:mockito-core:4.8.+"
    }
}

project(':backend-api') {
    bootJar { enabled = true }
    jar { enabled = false }
    dependencies {
        implementation project(':backend-core')
    }
}

project(':backend-batch') {
    bootJar { enabled = false }    // Todo 나중에 배치 쓰려면 bootJar true 로 설정해야함
    jar { enabled = false }
    dependencies {
        implementation project(':backend-core')
    }
}

project(':backend-core') {
    bootJar { enabled = false }
    jar { enabled = true }
}
